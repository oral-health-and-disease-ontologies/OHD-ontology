PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX inheres_in: <http://purl.obolibrary.org/obo/BFO_0000052>
PREFIX participates_in: <http://purl.obolibrary.org/obo/BFO_0000056>
PREFIX tooth_restoration_procedure: <http://purl.obolibrary.org/obo/OHD_0000004>
PREFIX tooth_to_be_restored_role: <http://purl.obolibrary.org/obo/OHD_0000007>
PREFIX realizes: <http://purl.obolibrary.org/obo/BFO_0000055>
PREFIX tooth: <http://purl.obolibrary.org/obo/FMA_12516>
PREFIX is_part_of: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX tooth_surface: <http://purl.obolibrary.org/obo/FMA_no_fmaid_Surface_enamel_of_tooth>
PREFIX dental_restoration_material: <http://purl.obolibrary.org/obo/OHD_0000000>
PREFIX tooth_number: <http://purl.obolibrary.org/obo/OHD_0000065>
PREFIX patient: <http://purl.obolibrary.org/obo/OHD_0000012>

select ?restoration_procedure (count(*) as ?count)
where 
{
  ## patient and patient's tooth
  ?patienti rdf:type patient: .  
  ?toothi rdf:type tooth: .
  ?toothi is_part_of: ?patienti .
  
  ## surfaces that are part of tooth
  ?surfacei rdf:type tooth_surface: .
  ?surfacei is_part_of: ?toothi .
  
  ## restoration procedure and tooth be restored role
  ?proct rdfs:subClassOf tooth_restoration_procedure: .
  ?proci rdf:type ?proct .
  ?rolei rdf:type tooth_to_be_restored_role: .
  
  ## the tooth to be restored role inheres in the tooth
  ## and is realized by the procedure
  ?rolei inheres_in: ?toothi .
  ?proci realizes: ?rolei .
  
  ## the tooth's surface participated in the procedure
  ?surfacei participates_in: ?proci .
  
  ## assign labels
  ?proct rdfs:label ?restoration_procedure .
  
}
group by ?restoration_procedure
order by desc(?count)

